#!/usr/bin/env expect
#
# Copyright (c) 2021-2022, Arm Limited.
#
# SPDX-License-Identifier: MIT

proc guest_vm_logon {guest} {
    global expect_out
    global spawn_id
    global login_prompt
    global prompt
    global clean_console
    global timeout

    spawn sudo -n xl console ${guest}
    send "${clean_console}"
    sleep 0.5

    set glob_timeout ${timeout}
    set timeout 5
    expect {
        -re {.} {
            exp_continue
        }
        timeout {
            send "${clean_console}"
            sleep 0.5
        }
    }
    set timeout ${glob_timeout}

    # Grab console and log on
    expect {
        "${login_prompt}" {
            send "root\r"
            sleep 1
            exp_continue
        }
        "${prompt}" {
            send "${clean_console}"
            sleep 0.5
        }
        "Password:" {
            send "\x03"
            sleep 5
            exp_continue
        }
        "Kernel panic" {
            puts "'${guest}' encountered a kernel panic"
            return 1
        }
        "${guest} is an invalid domain" {
            puts "'${guest}' is an invalid domain"
            return 1
        }
        timeout {
            puts "Log-in to '${guest}' timed out"
            return 124
        }
    }
    return 0
}

proc run_cmd {cmd} {
    global expect_out
    global spawn_id
    global prompt
    global clean_console
    global timeout

    send "${cmd}\r"
    sleep 0.5

    expect {
        "${prompt}" {
        }
        timeout {
            puts "Executing '${cmd}' exceeded the ${timeout}s time out."
            return 124
        }
    }

    send "echo Exit status: \$?\r"
    sleep 0.5

    # Set the return code of this process to that of the command
    set rc 1
    expect {
        {Exit status: 0} {
            set rc 0
        }
        -re {Exit status: [1-9]+} {
        }
    }

    expect "${prompt}"

    return $rc
}

proc guest_vm_logoff {} {
    global expect_out
    global spawn_id
    global login_prompt
    global clean_console
    global timeout

    # Exit the login session
    send "exit\r"
    sleep 0.5

    expect "${login_prompt}"
    # Send ctrl + ] to exit the Guest VM
    send "\x1b"

    return 0
}
