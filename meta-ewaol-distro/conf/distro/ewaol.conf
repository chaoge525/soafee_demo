# Copyright (c) 2021-2022, Arm Limited.
#
# SPDX-License-Identifier: MIT

require ${COREBASE}/meta-poky/conf/distro/poky.conf

# Introduce the EWAOL Yocto Distro
DISTRO = "ewaol"
DISTRO_NAME = "EWAOL (Edge Workload Abstraction and Orchestration Layer)"
DISTRO_VERSION = "unstable"

# systemd as the init system
INIT_MANAGER = "systemd"
DISTRO_FEATURES:append = " virtualization ipv6 seccomp k3s"

# Apply feature specific config
USER_CLASSES:append = " ewaol-distro-features"

# rpm as the package management system
PACKAGE_CLASSES ?= "package_rpm"

PREFERRED_PROVIDER_virtual/runc = "runc-opencontainers"
PREFERRED_PROVIDER_virtual/docker = "docker-ce"

# These classes produce warnings if there are any missing kernel configurations
# that are required by their target packages
KERNEL_CLASSES:append = " containers_kernelcfg_check k3s_kernelcfg_check"

# This variable is used to include ewaol distro feature specific settings
# e.g. for "ewaol-virtualization"
EWAOL_OVERRIDES ??= ""

# Default user account names and default admin group name
EWAOL_USER_ACCOUNT = "user"
EWAOL_ADMIN_ACCOUNT = "ewaol"
EWAOL_TEST_ACCOUNT = "test"
EWAOL_ADMIN_GROUP = "sudo"
EWAOL_ADMIN_GROUP_OPTIONS ?= "NOPASSWD:"

# Select whether or not to generate the filesystem able to run in any aarch64
# platform. If EWAOL_GENERIC_ARM64_FILESYSTEM is "1", armv8a-crc DEFAULTTUNE
# will be used otherwise the one set by the MACHINE conf will be selected.
EWAOL_GENERIC_ARM64_FILESYSTEM ?= "1"
EWAOL_GENERIC_ARM64_DEFAULTTUNE ?= "armv8a-crc"

# Use generic DEFAULTTUNE if generic filestystem enabled otherwise preserve
# DEFAULTTUNE value.
DEFAULTTUNE:aarch64 := "\
${@ d.getVar('EWAOL_GENERIC_ARM64_DEFAULTTUNE', False) \
    if (bb.utils.to_boolean(d.getVar('EWAOL_GENERIC_ARM64_FILESYSTEM', \
                                     True), False)) \
    else d.getVar('DEFAULTTUNE', False)}"
